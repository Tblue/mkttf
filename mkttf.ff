#!/usr/bin/fontforge -lang=ff
/*      This fontforge script converts a set of BDF files into a TTF font
 *      (and a SFD file).
 *      
 *      Copyright (c) 2009-2010 by Tilman Blumenbach <tilman@ax86.net>
 *      All rights reserved.
 *      
 *      Redistribution and use in source and binary forms, with or without
 *      modification, are permitted provided that the following conditions are
 *      met:
 *      
 *      * Redistributions of source code must retain the above copyright
 *        notice, this list of conditions and the following disclaimer.
 *      * Redistributions in binary form must reproduce the above
 *        copyright notice, this list of conditions and the following disclaimer
 *        in the documentation and/or other materials provided with the
 *        distribution.
 *      * Neither the name of the author nor the names of its contributors
 *        may be used to endorse or promote products derived from this
 *        software without specific prior written permission.
 *      
 *      THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 *      "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 *      LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 *      A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 *      OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 *      SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 *      LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 *      DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 *      THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 *      (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 *      OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

if( $argc < 3 )
	Print( "Usage:\n ", $argv[0], " font-family bdf-file...\n" );
	Print( "The BDF files have to be sorted by font size in ascending order." );
	Quit( 1 );
endif

Open( $argv[2] );

// Import the other BDF files into this font:
Print( "Importing ", $argc - 3, " additional fonts..." );
i = 3;
while( i < $argc )
	Import( $argv[i++] );
endloop

// Import the last BDF font into the glyph background:
Import( $argv[$argc - 1], 1 );

// AutoTrace all glyphs, add extrema and simplify:
Print( "Processing glyphs..." );
SelectAll();
foreach
	AutoTrace();
	AddExtrema();
	Simplify();
endloop

weight = "";
if( Strrstr( $fontname, "Italic" ) != -1 )
	// The weight should be italic:
	weight = "Italic";
endif

// Set font family name and weight:
SetFontNames( "", $argv[1], "", weight );

Print( "Saving..." );
Generate( $fontname + ".ttf", "ttf" );
Save( $fontname + ".sfd" );

Print( "Done." );
